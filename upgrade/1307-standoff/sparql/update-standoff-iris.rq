# Copyright @ 2015-2019 the contributors (see Contributors.md).
#
# This file is part of Knora.
#
# Knora is free software: you can redistribute it and/or modify
# it under the terms of the GNU Affero General Public License as published
# by the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# Knora is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU Affero General Public License for more details.
#
# You should have received a copy of the GNU Affero General Public
# License along with Knora.  If not, see <http://www.gnu.org/licenses/>.

PREFIX knora-base: <http://www.knora.org/ontology/knora-base#>

# Change the IRIs of standoff tags so the last component is the start index.
DELETE {
    GRAPH ?g {
        ?textValue knora-base:valueHasStandoff ?oldStandoffTag .
        ?oldStandoffTag ?standoffPred ?standoffObj .
        ?oldStandoffTag knora-base:standoffTagHasStartParent ?oldStartParent .
        ?oldStandoffTag knora-base:standoffTagHasEndParent ?oldEndParent .
    }
} INSERT {
    GRAPH ?g {
        ?textValue knora-base:valueHasStandoff ?newStandoffTag .
        ?newStandoffTag ?standoffPred ?standoffObj .
        ?newStandoffTag knora-base:standoffTagHasStartParent ?newStartParent .
        ?newStandoffTag knora-base:standoffTagHasEndParent ?newEndParent .
    }
}
USING <http://www.ontotext.com/explicit>
WHERE {
    {
        SELECT ?textValue ?oldStandoffTag ?newStandoffTag WHERE {
            GRAPH ?g {
                ?textValue knora-base:valueHasStandoff ?oldStandoffTag .
                ?oldStandoffTag knora-base:standoffTagHasStartIndex ?startIndex .
            }

            BIND(IRI(CONCAT(STR(?textValue), "/standoff/", STR(?startIndex))) AS ?newStandoffTag)
        }
    }

    GRAPH ?g {
        ?oldStandoffTag ?standoffPred ?standoffObj .
    }

    FILTER(?standoffPred != knora-base:standoffTagHasStartParent && ?standoffPred != knora-base:standoffTagHasEndParent)

    OPTIONAL {
        GRAPH ?g {
            ?oldStandoffTag knora-base:standoffTagHasStartParent ?oldStartParent .
            ?oldStartParent knora-base:standoffTagHasStartIndex ?oldStartParentStartIndex .
        }

        BIND(IRI(CONCAT(STR(?textValue), "/standoff/", STR(?oldStartParentStartIndex))) AS ?newStartParent)
    }

    OPTIONAL {
        GRAPH ?g {
            ?oldStandoffTag knora-base:standoffTagHasEndParent ?oldEndParent .
            ?oldEndParent knora-base:standoffTagHasStartIndex ?oldEndParentStartIndex .
        }

        BIND(IRI(CONCAT(STR(?textValue), "/standoff/", STR(?oldEndParentStartIndex))) AS ?newEndParent)
    }
}
