PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX knora-admin: <http://www.knora.org/ontology/knora-admin#>
PREFIX knora-base: <http://www.knora.org/ontology/knora-base#>

DELETE {
    GRAPH <http://www.knora.org/ontology/knora-base> {
        ?oldKnoraBaseSubj ?oldKnoraBasePred ?oldKnoraBaseObj .
    }

    GRAPH ?graph1 {
        ?subj1 ?oldPred1 ?oldObj1 .
    }

    GRAPH ?graph2 {
        ?subj2 ?oldPred2 ?obj2 .
    }

    GRAPH ?graph3 {
        ?subj3 ?pred3 ?oldObj3 .
    }

    GRAPH ?graph4 {
        ?subj4 knora-base:hasPermissions ?oldPermissions .
    }
} INSERT {
    GRAPH <http://www.knora.org/ontology/knora-admin> {
        $knoraAdmin
    }

    GRAPH <http://www.knora.org/ontology/knora-base> {
        $knoraBase
    }

    GRAPH ?graph1 {
        ?subj1 ?newPred1 ?newObj1 .
    }

    GRAPH ?graph2 {
        ?subj2 ?newPred2 ?obj2 .
    }

    GRAPH ?graph3 {
        ?subj3 ?pred3 ?newObj3 .
    }

    GRAPH ?graph4 {
        ?subj4 knora-base:hasPermissions ?newPermissions .
    }
} WHERE {
    {
        GRAPH <http://www.knora.org/ontology/knora-base> {
            ?oldKnoraBaseSubj ?oldKnoraBasePred ?oldKnoraBaseObj .
        }
    } UNION {
        VALUES (?oldPred1 ?oldObj1 ?newPred1 ?newObj1) { $preds_with_objs } .

        GRAPH ?graph1 {
            ?subj1 ?oldPred1 ?oldObj1 .
        }

        FILTER (?graph1 != <http://www.knora.org/ontology/knora-admin> && ?graph1 != <http://www.knora.org/ontology/knora-base>)
    } UNION {
        VALUES (?oldPred2 ?newPred2) { $predicates } .

        GRAPH ?graph2 {
            ?subj2 ?oldPred2 ?obj2 .
        }

        FILTER (?graph2 != <http://www.knora.org/ontology/knora-admin> && ?graph2 != <http://www.knora.org/ontology/knora-base>)
    } UNION {
        VALUES (?oldObj3 ?newObj3) { $objects } .

        GRAPH ?graph3 {
            ?subj3 ?pred3 ?oldObj3 .
        }

        FILTER (?graph3 != <http://www.knora.org/ontology/knora-admin> && ?graph3 != <http://www.knora.org/ontology/knora-base>)
    } UNION {
        GRAPH ?graph4 {
            ?subj4 knora-base:hasPermissions ?oldPermissions .
        }

        FILTER (?graph4 != <http://www.knora.org/ontology/knora-admin> && ?graph4 != <http://www.knora.org/ontology/knora-base>)
        FILTER(REGEX(?oldPermissions, "knora-base"))
        BIND(REPLACE(?oldPermissions, "knora-base", "knora-admin") AS ?newPermissions)
    }
}
