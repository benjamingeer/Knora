# Copyright @ 2015-2019 the contributors (see Contributors.md).
#
# This file is part of Knora.
#
# Knora is free software: you can redistribute it and/or modify
# it under the terms of the GNU Affero General Public License as published
# by the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# Knora is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU Affero General Public License for more details.
#
# You should have received a copy of the GNU Affero General Public
# License along with Knora.  If not, see <http://www.gnu.org/licenses/>.

PREFIX knora-base: <http://www.knora.org/ontology/knora-base#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

# Delete obsolete preview images and preview-related knora-base definitions
# from the triplestore.
DELETE {
    ?class rdfs:subClassOf ?restriction .
    ?restriction ?restrictionPred ?restrictionObj .
    ?previewValue ?previewValuePred ?previewValueObj .
    ?resource ?resourcePred ?previewValue .
    ?fullSize knora-base:qualityLevel ?qualityLevel .
    ?fullSize knora-base:isPreview ?preview .
    ?fullSize knora-base:valueHasQname ?qName .
}
USING <http://www.ontotext.com/explicit>
WHERE {
    {
        # Delete qualityLevel and isPreview cardinalities, and unlink them from class definitions.
        ?class rdfs:subClassOf ?restriction .

        ?restriction rdf:type owl:Restriction ;
            owl:onProperty ?onProp ;
            ?restrictionPred ?restrictionObj .

        FILTER(?onProp = knora-base:qualityLevel || ?onProp = knora-base:isPreview)
    } UNION {
        # Delete preview images and unlink them from resources.
        ?previewValue a knora-base:StillImageFileValue ;
            knora-base:isPreview true ;
            ?previewValuePred ?previewValueObj .

        ?resource ?resourcePred ?previewValue .
    } UNION {
        # Remove qualityLevel from full-size images.
        ?fullSize knora-base:qualityLevel ?qualityLevel .
    } UNION {
        # Remove isPreview from full-size images.
        ?fullSize knora-base:isPreview ?preview .
    } UNION {
        # Remove valueHasQname from full-size images.
        ?fullSize knora-base:valueHasQname ?qName .
    }
};
# Use owl:cardinality 1 on knora-base:hasFileValue and its subproperties.
DELETE {
    GRAPH ?graph {
        ?restriction owl:minCardinality "1"^^xsd:nonNegativeInteger .
    }
} INSERT {
    GRAPH ?graph {
	    ?restriction owl:cardinality "1"^^xsd:nonNegativeInteger .
    }
}
USING <http://www.ontotext.com/explicit>
WHERE {
    GRAPH ?graph {
        ?restriction owl:minCardinality "1"^^xsd:nonNegativeInteger ;
            owl:onProperty ?prop .

        ?prop rdfs:subPropertyOf* knora-base:hasFileValue .
    }
}
