@*
 * Copyright Â© 2021 - 2023 Swiss National Data and Service Center for the Humanities and/or DaSCH Service Platform contributors.
 * SPDX-License-Identifier: Apache-2.0
 *@

@import org.knora.webapi.IRI

@**
 * Returns the permission-relevant assertions on the subject and object of a link value.
 *
 * @param linkValueIri the IRI of the link value.
 *@
@(linkValueIri: IRI)

prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix knora-base: <http://www.knora.org/ontology/knora-base#>

SELECT ?source ?sourceCreator ?sourceProject ?sourcePermissions ?target ?targetCreator ?targetProject ?targetPermissions

WHERE {
    BIND(IRI("@linkValueIri") AS ?linkValue)

    ?linkValue rdf:type knora-base:LinkValue ;
               rdf:subject ?source ;
               rdf:predicate ?predicate ;
               rdf:object ?target ;
               knora-base:isDeleted false .

    ?source knora-base:attachedToUser ?sourceCreator ;
             knora-base:attachedToProject ?sourceProject ;
             knora-base:hasPermissions ?sourcePermissions ;
             knora-base:isDeleted false .

    ?target knora-base:attachedToUser ?targetCreator ;
            knora-base:attachedToProject ?targetProject ;
            knora-base:hasPermissions ?targetPermissions ;
            knora-base:isDeleted false .
}
