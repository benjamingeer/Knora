@*
 * Copyright Â© 2021 - 2023 Swiss National Data and Service Center for the Humanities and/or DaSCH Service Platform contributors.
 * SPDX-License-Identifier: Apache-2.0
 *@

@import org.knora.webapi.IRI

@**
 * Given the IRI of a resource, finds any other resource that contains the specified resource
 * using SPARQL w/o inference.
 *
 * @param resourceIri the resource whose containing resource, if any, is to be returned.
 *@
@(resourceIri: IRI)

prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix knora-base: <http://www.knora.org/ontology/knora-base#>

SELECT ?containingResource ?containingResourceProject ?linkValue ?linkValueCreator ?linkValuePermissions
WHERE {
    BIND(IRI("@resourceIri") as ?resource)

    ?linkingProp rdfs:subPropertyOf* knora-base:isPartOf .
    ?resource ?linkingProp ?containingResource .
    ?containingResource knora-base:isDeleted false ;
        knora-base:attachedToProject ?containingResourceProject .

    FILTER NOT EXISTS {
        ?resource knora-base:isDeleted true .
    }

    BIND(IRI(CONCAT(STR(?linkingProp), "Value")) AS ?linkValueProp)

    ?resource ?linkValueProp ?linkValue .

    ?linkValue rdf:type knora-base:LinkValue ;
        rdf:subject ?resource ;
        rdf:predicate ?linkingProp ;
        rdf:object ?containingResource ;
        knora-base:attachedToUser ?linkValueCreator ;
        knora-base:hasPermissions ?linkValuePermissions .

    FILTER NOT EXISTS {
        ?linkValue knora-base:isDeleted true .
    }
}
LIMIT 1
