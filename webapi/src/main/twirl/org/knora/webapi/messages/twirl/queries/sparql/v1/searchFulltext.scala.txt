@*
 * Copyright Â© 2021 - 2022 Swiss National Data and Service Center for the Humanities and/or DaSCH Service Platform contributors.
 * SPDX-License-Identifier: Apache-2.0
 *@

@import org.knora.webapi.IRI
@import org.knora.webapi.exceptions.SparqlGenerationException
@import org.knora.webapi.util.ApacheLuceneSupport.LuceneQueryString

@*
 * Performs a simple full-text search using SPARQL w/o inference.
 *
 * The number of rows returned per matching resource is equal to the number of values that matched in the resource,
 * plus one if the resource's label matched.
 *
 * @param searchTerms search terms to be combined with AND in Lucene full-text search syntax.
 * @param preferredLanguage the language code of the user's preferred language.
 * @param fallbackLanguage the language code of the application's default language.
 * @param projectIriOption if filtering by project is desired, the IRI of the project to search.
 * @param restypeIriOption if given, searches only for resources of this class and its subclasses.
 *@
@(searchTerms: LuceneQueryString,
  preferredLanguage: String,
  fallbackLanguage: String,
  projectIriOption: Option[IRI],
  restypeIriOption: Option[IRI])

prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix knora-base: <http://www.knora.org/ontology/knora-base#>

SELECT DISTINCT
    ?resource
    ?resourceLabel @* The label of a matching resource. This is called "firstprop" in the v1 API. *@
    ?resourceClass
    ?previewPath
    ?previewDimX
    ?previewDimY
    ?resourceCreator
    ?resourceProject
    ?resourcePermissions
    ?resourceProperty
    ?valueObject
    ?valueObjectType
    ?valueCreator
    ?valuePermissions
    ?literal
WHERE {
    BIND(STR("@preferredLanguage") AS ?preferredLanguage)
    BIND(STR("@fallbackLanguage") AS ?fallbackLanguage)

    ?matchingSubject <http://jena.apache.org/text#query> '@searchTerms.getQueryString' .

    FILTER NOT EXISTS {
        ?matchingSubject knora-base:isDeleted true .
    }

    # ?matchingSubject could be a resource (whose rdfs:label matched) or a value object

    OPTIONAL {
        # If this clause is executed, it is a value object. We set ?containingResource to the resource that contains the matching value object.

        ?matchingSubject a ?valueObjectType .
        ?valueObjectType rdfs:subClassOf* knora-base:Value .
        ?containingResource ?resourceProperty ?matchingSubject .
        ?resourceProperty rdfs:subPropertyOf* knora-base:hasValue .

        FILTER(?valueObjectType != knora-base:LinkValue)

        FILTER NOT EXISTS {
            ?containingResource knora-base:isDeleted true .
        }

        BIND(?matchingSubject AS ?valueObject)

        ?valueObject knora-base:attachedToUser ?valueCreator ;
            knora-base:hasPermissions ?valuePermissions .

        ?valueObject knora-base:valueHasString ?literal .
    }

    # If the previous OPTIONAL clause executed, ?matchingSubject is a value, and ?containingResource will be set. Otherwise, ?matchingSubject is a resource.
    BIND(COALESCE(?containingResource, ?matchingSubject) AS ?resource)

    @projectIriOption match {
        case Some(projectIri) => {

            # filter by projectIri
            ?resource knora-base:attachedToProject <@projectIri> .

        }

        case None => {}
    }

    ?resource a ?resourceClass .

    @restypeIriOption match {

        case Some(restypeIri) => {

            # Filter by resource class.

            ?resClass rdfs:subClassOf* <@restypeIri> .
            ?resource a ?resClass .

        }

        case None => {

            @* Ensure that each matching resource is a knora-base:Resource. *@

            ?resClass rdfs:subClassOf* knora-base:Resource .
            ?resource a ?resClass .

        }
    }

    ?resource rdfs:label ?resourceLabel .

    OPTIONAL {
       ?resource knora-base:hasStillImageFileValue ?fileValue .
       ?fileValue a knora-base:StillImageFileValue .
       ?fileValue knora-base:internalFilename ?previewPath .

        OPTIONAL {
            ?fileValue knora-base:dimX ?previewDimX .
            ?fileValue knora-base:dimY ?previewDimY .
        }
    }

    ?resource knora-base:attachedToUser ?resourceCreator ;
        knora-base:attachedToProject ?resourceProject ;
        knora-base:hasPermissions ?resourcePermissions .
}
LIMIT 500
