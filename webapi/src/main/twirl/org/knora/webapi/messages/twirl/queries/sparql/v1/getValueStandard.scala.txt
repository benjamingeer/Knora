@*
 * Copyright Â© 2021 Data and Service Center for the Humanities and/or DaSCH Service Platform contributors.
 * SPDX-License-Identifier: Apache-2.0
 *@

@import org.knora.webapi.IRI

@**
 * Queries a knora-base:Value using standard SPARQL, without inference.
 *
 * This template is used only by getValue.scala.txt.
 *
 * @param triplestore the name of the triplestore being used.
 * @param iri the IRI of the Value.
 *@
@(triplestore: String,
  valueIri: IRI)

prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix knora-base: <http://www.knora.org/ontology/knora-base#>

SELECT ?objPred ?objObj ?predStandoff ?objStandoff
WHERE {
    BIND(IRI("@valueIri") AS ?obj)

    ?resourceProp rdfs:subPropertyOf* knora-base:hasValue .

    {
        ?resource ?resourceProp ?obj .

        FILTER NOT EXISTS {
            ?resource knora-base:isDeleted true .
        }

        FILTER NOT EXISTS {
            ?obj knora-base:isDeleted true .
        }

        ?obj ?objPred ?objObj .

        FILTER(?objPred != knora-base:attachedToProject)

        OPTIONAL {
            FILTER(?objPred = knora-base:valueHasStandoff)
            ?objObj ?predStandoff ?objStandoff .
        }
    }
    UNION
    {
        @* Return the project of the resource that contains the value. *@

        ?resource ?resourceProp ?obj .

        FILTER NOT EXISTS {
            ?resource knora-base:isDeleted true .
        }

        FILTER NOT EXISTS {
            ?obj knora-base:isDeleted true .
        }

        ?resource knora-base:attachedToProject ?resourceProject .

        BIND(knora-base:attachedToProject AS ?objPred)
        BIND(?resourceProject AS ?objObj)
    }
}
