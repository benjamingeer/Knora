@*
 * Copyright Â© 2021 - 2022 Swiss National Data and Service Center for the Humanities and/or DaSCH Service Platform contributors.
 * SPDX-License-Identifier: Apache-2.0
 *@

@import org.knora.webapi.IRI

@**
 * Returns all versions of a value using SPARQL w/o inference.
 *
 * @param resourceIri the resource to search.
 * @param propertyIri the property to search.
 * @param currentValueIri the most recent version of the value.
 *
 * Returns nothing if currentValueIri is not the most recent version.
 *@
 @(resourceIri: IRI,
   propertyIri: IRI,
   currentValueIri: IRI)

prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix knora-base: <http://www.knora.org/ontology/knora-base#>

SELECT ?value ?valueCreationDate ?previousValue ?isLinkValue ?linkValuePredicate ?valueCreator ?project ?valuePermissions
WHERE {
    BIND(IRI("@resourceIri") AS ?resource)
    BIND(IRI("@propertyIri") AS ?prop)
    BIND(IRI("@currentValueIri") AS ?currentValue)

    ?currentValue knora-base:hasPermissions ?valuePermissions .

    FILTER NOT EXISTS {
        ?currentValue knora-base:isDeleted true .
    }

    ?resource ?prop ?currentValue .
    ?prop rdfs:subPropertyOf* knora-base:hasValue .
    ?resource knora-base:attachedToProject ?project .

    ?currentValue knora-base:previousValue* ?value .

    FILTER NOT EXISTS {
        ?resource knora-base:isDeleted true .
    }

    OPTIONAL {
        ?value knora-base:valueCreationDate ?valueCreationDate .
    }

    OPTIONAL {
        ?value knora-base:previousValue ?previousValue .
    }

    OPTIONAL {
        ?value rdf:type knora-base:LinkValue .
        ?value rdf:predicate ?linkValuePredicate .
        BIND(true as ?isLinkValue)
    }

    ?value knora-base:attachedToUser ?valueCreator .
}
