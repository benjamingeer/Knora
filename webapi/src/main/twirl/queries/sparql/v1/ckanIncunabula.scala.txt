@*
 * Copyright Â© 2021 - 2022 Swiss National Data and Service Center for the Humanities and/or DaSCH Service Platform contributors.
 * SPDX-License-Identifier: Apache-2.0
 *@

@import org.knora.webapi.IRI

@**
 * Gets all Incunabula books with their pages.
 *
 * @param triplestore the name of the triplestore being used.
 * @param projectIri the IRI of the Incunabula project.
 * @param limitOption the maximum number of rows to return.
 *@
@(triplestore: String,
  projectIri: IRI,
  limitOption: Option[Int])

prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix knora-base: <http://www.knora.org/ontology/knora-base#>
prefix incunabula: <http://www.knora.org/ontology/0803/incunabula#>

SELECT ?book ?page
@* Ensure that inference is not used in this query. *@
@if(triplestore.startsWith("graphdb")) {
    FROM <http://www.ontotext.com/explicit>
}
WHERE {
    BIND(IRI("@projectIri") as ?projectIri)

    ?book knora-base:attachedToProject ?projectIri ;
        rdf:type incunabula:book ;
        knora-base:isDeleted false .

    ?page incunabula:partOf ?book ;
        knora-base:isDeleted false .
}
@limitOption match {
    case Some(limit) => {
        LIMIT @limit
    }

    case None => {}
}
