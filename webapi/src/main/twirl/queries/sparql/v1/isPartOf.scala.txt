@*
 * Copyright © 2015 Lukas Rosenthaler, Benjamin Geer, Ivan Subotic,
 * Tobias Schweizer, André Kilchenmann, and André Fatton.
 *
 * This file is part of Knora.
 *
 * Knora is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Affero General Public License as published
 * by the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * Knora is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Affero General Public License for more details.
 *
 * You should have received a copy of the GNU Affero General Public
 * License along with Knora.  If not, see <http://www.gnu.org/licenses/>.
 *@

@import org.knora.webapi.IRI

@**
 * Given the IRI of a resource, finds any other resource that contains the specified resource.
 *
 * @param triplestore the name of the triplestore being used.
 * @param resourceIri the resource whose containing resource, if any, is to be returned.
 *@
@(triplestore: String,
  resourceIri: IRI)

prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix knora-base: <http://www.knora.org/ontology/knora-base#>

SELECT ?containingResource ?containingResourceProject ?linkValue ?linkValueOwner ?linkValueProject ?linkValuePermissions
@* Ensure that inference is not used in this query. *@
@if(triplestore.startsWith("graphdb")) {
    FROM <http://www.ontotext.com/explicit>
}
WHERE {
    BIND(IRI("@resourceIri") as ?resource)

    ?resource knora-base:isDeleted false .
    ?linkingProp rdfs:subPropertyOf+ knora-base:isPartOf .
    ?resource ?linkingProp ?containingResource .
    ?containingResource knora-base:isDeleted false ;
        knora-base:attachedToProject ?containingResourceProject .

    BIND(IRI(CONCAT(STR(?linkingProp), "Value")) AS ?linkValueProp)

    ?resource ?linkValueProp ?linkValue .

    ?linkValue rdf:type knora-base:LinkValue ;
        rdf:subject ?resource ;
        rdf:predicate ?linkingProp ;
        rdf:object ?containingResource ;
        knora-base:isDeleted false ;
        knora-base:attachedToUser ?linkValueOwner .

    OPTIONAL {
        ?linkValue knora-base:attachedToProject ?linkValueProject .
    }

    OPTIONAL {
        ?linkValue knora-base:hasPermissions ?linkValuePermissions .
    }
}
LIMIT 1
